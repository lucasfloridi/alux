#INCLUDE "TOTVS.CH"
#INCLUDE "PROTHEUS.CH"

/*/{Protheus.doc} XMTR860
Relacao Das Ordens de Producao.
Adaptado do fonte padrao MATR860 (TOTVS) para uso como User Function.

@type    User Function
@author  Felipe Nunes Toledo (original) / Leonardo Barboza Ribeiro Leite (adaptacao)
@since   23/06/2006 - Adaptado em 26/05/2026
@version 12.1.2410
@return  NIL
/*/
User Function XMTR860()
Local lRetMsg     := .T.
Local oReport

Private cQryOP

If lRetMsg
	// Funcao utilizada para verificar a ultima versao dos fontes
	// SIGACUS.PRW, SIGACUSA.PRX e SIGACUSB.PRX, aplicados no rpo do
	// cliente, assim verificando a necessidade de uma atualizacao
	// nestes fontes. NAO REMOVER !!!
	If !(FindFunction("MATA330_V")	.And. MATA330_V() >= 20091212)
		Aviso("Atencao","Atualizar MATA330.PRX !!!",{"OK"})	//"Atencao"##"Atualizar MATA330.PRX !!!"##"OK"
		Return
	EndIf

	If TRepInUse()
		// Interface de impressao
		oReport:= ReportDef()
		oReport:PrintDialog()
	Else
		MATR860R3()
	EndIf
EndIf

Return NIL

/*/{Protheus.doc} ReportDef
A funcao estatica ReportDef devera ser criada para todos os
relatorios que poderao ser agendados pelo usuario.

@type   Static Function
@author Felipe Nunes Toledo
@since  23/06/2006
@return oReport, Object, Objeto TReport configurado
@uses   XMTR860 / MATR860
/*/
Static Function ReportDef()
Local oReport
Local oSection1
Local nTamB1COD  := TamSX3('B1_COD')[1] * 2
Local nTamB1DESC := TamSX3('B1_DESC')[1]

nTamB1DESC := IIF(nTamB1DESC > 50, nTamB1DESC / 2, nTamB1DESC)

// Criacao do componente de impressao
//
// TReport():New
// ExpC1 : Nome do relatorio
// ExpC2 : Titulo
// ExpC3 : Pergunte
// ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao
// ExpC5 : Descricao
oReport:= TReport():New("MATR860",OemToAnsi("Relacao por Ordem de Producao"),"MTR860", {|oReport| ReportPrint(oReport)},OemToAnsi("O objetivo deste relatorio e exibir detalhadamente todas as movimenta-")+OemToAnsi("coes feitas para cada Ordem de Producao, mostrando inclusive os custos.")) //##"O objetivo deste relatorio e exibir detalhadamente todas as movimenta-coes feitas para cada Ordem de Producao, mostrando inclusive os custos."
oReport:SetLandscape() //Define a orientacao de pagina do relatorio como paisagem.

// Verifica as perguntas selecionadas (MTR860)
//
// Variaveis utilizadas para parametros
// mv_par01     // OP inicial
// mv_par02     // OP final
// mv_par03     // moeda selecionada ( 1 a 5 )
// mv_par04     // De  Data Movimentacao
// mv_par05     // Ate Data Movimentacao
// mv_par06     // Totaliza Mov.do mat. movimentados pela O.P.
// mv_par07     // Qual Custo Imprimir ? ( Medio / Reposicao )
Pergunte(oReport:uParam,.F.)

// Criacao da secao utilizada pelo relatorio
//
// TRSection():New
// ExpO1 : Objeto TReport que a secao pertence
// ExpC2 : Descricao da secao
// ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela
//         sera considerada como principal para a secao.
// ExpA4 : Array com as Ordens do relatorio
// ExpL5 : Carrega campos do SX3 como celulas - Default : False
// ExpL6 : Carrega ordens do Sindex - Default : False

// Secao 1 (oSection1)
oSection1 := TRSection():New(oReport,"Ordens de Producao",{"SD3","SB1"},/*Ordem*/) //"Ordens de Producao"
oSection1:SetHeaderPage()
oSection1:SetTotalInLine(.F.)

TRCell():New(oSection1,'D3_CC'    	,'SD3',/*Titulo*/,/*Picture*/					 ,/*Tamanho*/,  /*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,'D3_OP'	  	,'SD3',/*Titulo*/,/*Picture*/					 ,/*Tamanho*/,  /*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,'D3_CF'   	,'SD3',/*Titulo*/,/*Picture*/					 ,/*Tamanho*/,  /*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,'D3_COD'     ,'SD3',/*Titulo*/,"@!"                           ,nTamB1COD  ,  /*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,'B1_DESC'  	,'SB1',/*Titulo*/,/*Picture*/					 ,nTamB1DESC ,  /*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,'D3_QUANT' 	,'SD3',/*Titulo*/,/*Picture*/					 ,/*Tamanho*/,  /*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,'D3_UM'    	,'SD3', "U.M."  ,/*Picture*/					 ,/*Tamanho*/,  /*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,'Unitario'  	,'SD3', "Custo Unit."  ,PesqPict("SD3","D3_CUSTO1",17) ,/*Tamanho*/,  /*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,'Custo'   	,'SD3', "Custo Total"  ,PesqPict("SD3","D3_CUSTO1",17) ,/*Tamanho*/,  /*lPixel*/,/*{|| code-block de impressao }*/)
oSection1:Cell("Custo"):GetFieldInfo("D3_CUSTO1")
TRCell():New(oSection1,'D3_DOC'   	,'SD3',/*Titulo*/,/*Picture*/					 ,/*Tamanho*/,  /*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,'D3_EMISSAO'	,'SD3',/*Titulo*/,/*Picture*/					 ,/*Tamanho*/,  /*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,'B1_CUSTD'	,'SB1',/*Titulo*/,/*Picture*/					 ,/*Tamanho*/,  /*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,'TotalSTD'   ,'SB1',/*Titulo*/,PesqPict("SB1","B1_CUSTD",17)	 ,/*Tamanho*/,  /*lPixel*/,/*{|| code-block de impressao }*/)

Return(oReport)

/*/{Protheus.doc} ReportPrint
A funcao estatica ReportPrint devera ser criada para todos
os relatorios que poderao ser agendados pelo usuario.

@type   Static Function
@author Felipe Nunes Toledo
@since  23/06/2006
@param  oReport, Object, Objeto Report do Relatorio
@return NIL
@uses   XMTR860 / MATR860
/*/
Static Function ReportPrint(oReport)
Local oSection1 := oReport:Section(1)
Local dDataFec	:= GETMV("MV_ULMES")
Local cOrderBy  := ''
Local cCampoCus := ''
Local oBreak, oBreak2
Local oFunction
Local cOpAnt
// MV_CUSREP - Parametro utilizado para habilitar o calculo do
//             Custo de Reposicao.
Local lCusRep  := SuperGetMv("MV_CUSREP",.F.,.F.) .And. (MA330AvRep())

// Definicao do titulo do relatorio
oReport:SetTitle("Relacao por Ordem de Producao"+" - "+AllTrim(GetMv("MV_SIMB"+Ltrim(Str(mv_par03))))+ " / " + IIf(lCusRep .And. mv_par07==2,"Custo Reposicao","Custo Medio") )

// Alerta o usuario que o custo de reposicao esta desativado.
If mv_par07==2 .And. !lCusRep
	Help(" ",1,"A860CUSRP")
	mv_par07 := 1
EndIf

// Filtragem do relatorio

// Transforma parametros Range em expressao SQL
MakeSqlExpr(oReport:uParam)

// Query do relatorio da secao 1

oSection1:BeginQuery()

cQryOP    := GetNextAlias()

cOrderBy := "% D3_FILIAL, D3_OP, D3_CHAVE, D3_NUMSEQ, D3_COD  %"

BeginSql Alias cQryOP

SELECT SD3.*,
       SB1.B1_COD, SB1.B1_DESC, SB1.B1_CUSTD,
       CASE WHEN SUBSTRING(SB1.B1_COD,1,3) = 'MOD' OR SB1.B1_CCCUSTO <> ' ' THEN 'S' ELSE 'N' END ISMOD

FROM %table:SD3% SD3, %table:SB1% SB1

WHERE SD3.D3_FILIAL   = %xFilial:SD3%	 AND
      SB1.B1_FILIAL   = %xFilial:SB1%	 AND
	  SD3.D3_COD      = SB1.B1_COD     	 AND
	  SD3.D3_OP    	 >= %Exp:mv_par01%	 AND
 	  SD3.D3_OP      <= %Exp:mv_par02%	 AND
 	  SD3.D3_EMISSAO >= %Exp:mv_par04%	 AND
 	  SD3.D3_EMISSAO <= %Exp:mv_par05%	 AND
	  SD3.D3_OP      <> ' '            	 AND
	  D3_ESTORNO     <> 'S'           	 AND
	  SD3.%NotDel%						 AND
	  SB1.%NotDel%

ORDER BY %Exp:cOrderBy%

EndSql

oSection1:EndQuery()

// Totalizando a OP conforme parametro MV_PAR01
If mv_par06 == 1
	oBreak    := TRBreak():New(oSection1,oSection1:Cell("D3_OP"),NIL,.F.)
	oFunction := TRFunction():New(oSection1:Cell('D3_QUANT'),NIL,"SUM",oBreak,NIL,/*Picture*/,{|| If((cQryOP)->ISMOD == 'N' .And. SubStr((cQryOP)->D3_CF,1,2)<>"PR", oSection1:Cell('D3_QUANT'):GetValue(), 0) },.F.,.F.)
	oFunction := TRFunction():New(oSection1:Cell('Custo'   ),NIL,"SUM",oBreak,NIL,/*Picture*/,{|| If((cQryOP)->ISMOD == 'N' .And. SubStr((cQryOP)->D3_CF,1,2)<>"PR", oSection1:Cell('Custo'   ):GetValue(), 0) },.F.,.F.)
EndIf
// Definindo a Quebra Por Ordem de Producao
oBreak2   := TRBreak():New(oSection1,oSection1:Cell("D3_OP"),"Total OP",.T.) //"Total OP"

// Totalizando Por Ordem de Producao
If !(lCusRep .And. mv_par07==2)
	oFunction := TRFunction():New(oSection1:Cell('B1_CUSTD'),NIL,"MAX",oBreak2,"Custo Unit STD -----",/*Picture*/,/*uFormula*/,.F.,.F.) //"Custo Unit STD -----"
	oFunction := TRFunction():New(oSection1:Cell('TotalSTD'),NIL,"SUM",oBreak2,"Custo Total STD ----",/*Picture*/,/*uFormula*/,.F.,.F.) //"Custo Total STD ----"
EndIf
oFunction := TRFunction():New(oSection1:Cell('D3_QUANT'),NIL,"SUM",oBreak2,"Qtd. Mao de Obra ---",/*Picture*/,{|| If((cQryOP)->ISMOD == 'S', oSection1:Cell('D3_QUANT'):GetValue() , 0 ) },.F.,.F.) //"Qtd. Mao de Obra ---"
oFunction := TRFunction():New(oSection1:Cell('Custo'   ),NIL,"SUM",oBreak2,"Custo Mao de Obra --",/*Picture*/,{|| If((cQryOP)->ISMOD == 'S', oSection1:Cell('Custo'):GetValue()    , 0 ) },.F.,.F.) //"Custo Mao de Obra --"

// Totais Gerais
oFunction := TRFunction():New(oSection1:Cell('D3_QUANT'),NIL,"SUM",NIL,"QTDE. TOTAL REQUISICOES ------->",/*Picture*/,{|| If((cQryOP)->ISMOD == 'N' .And. SubStr((cQryOP)->D3_CF,1,2)=="RE" .And. SubStr(D3_CF,3,1) # "9", oSection1:Cell('D3_QUANT'):GetValue(), 0) },.F.,.T.) //"QTDE. TOTAL REQUISICOES ------->"
oFunction := TRFunction():New(oSection1:Cell('Custo'   ),NIL,"SUM",NIL,"CUSTO TOTAL REQUISICOES ------->",PesqPict("SD3","D3_CUSTO1",17),{|| If((cQryOP)->ISMOD == 'N' .And. SubStr((cQryOP)->D3_CF,1,2)=="RE" .And. SubStr(D3_CF,3,1) # "9", oSection1:Cell('Custo'   ):GetValue(), 0) },.F.,.T.) //"CUSTO TOTAL REQUISICOES ------->"

oFunction := TRFunction():New(oSection1:Cell('D3_QUANT'),NIL,"SUM",NIL,"QTDE. TOTAL PRODUCAO ---------->",/*Picture*/,{|| If(SubStr(D3_CF,1,2)=="PR",oSection1:Cell('D3_QUANT'):GetValue(),0) },.F.,.T.) //"QTDE. TOTAL PRODUCAO ---------->"
oFunction := TRFunction():New(oSection1:Cell('Custo'   ),NIL,"SUM",NIL,"CUSTO TOTAL PRODUCAO ---------->",PesqPict("SD3","D3_CUSTO1",17),{|| If(SubStr(D3_CF,1,2)=="PR",oSection1:Cell('Custo'   ):GetValue(),0) },.F.,.T.) //"CUSTO TOTAL PRODUCAO ---------->"

oFunction := TRFunction():New(oSection1:Cell('D3_QUANT'),NIL,"SUM",NIL,"QTDE. TOTAL DEVOLUCOES  ------->",/*Picture*/,{|| If((cQryOP)->ISMOD == 'N' .And. SubStr((cQryOP)->D3_CF,1,2)=="DE" .And. SubStr(D3_CF,3,1) # "9", (- oSection1:Cell('D3_QUANT'):GetValue()), 0) },.F.,.T.) //"QTDE. TOTAL DEVOLUCOES  ------->"
oFunction := TRFunction():New(oSection1:Cell('Custo'   ),NIL,"SUM",NIL,"CUSTO TOTAL DEVOLUCOES  ------->",PesqPict("SD3","D3_CUSTO1",17),{|| If((cQryOP)->ISMOD == 'N' .And. SubStr((cQryOP)->D3_CF,1,2)=="DE" .And. SubStr(D3_CF,3,1) # "9", (- oSection1:Cell('Custo'   ):GetValue()), 0) },.F.,.T.) //"CUSTO TOTAL DEVOLUCOES  ------->"

oFunction := TRFunction():New(oSection1:Cell('D3_QUANT'),NIL,"SUM",NIL,"QTDE. TOTAL REQ PODER 3 ------->",/*Picture*/,{|| If((cQryOP)->ISMOD == 'N' .And. SubStr((cQryOP)->D3_CF,1,2)=="RE" .And. SubStr(D3_CF,3,1)=="9", oSection1:Cell('D3_QUANT'):GetValue(), 0) },.F.,.T.) //"QTDE. TOTAL REQ PODER 3 ------->"
oFunction := TRFunction():New(oSection1:Cell('Custo'   ),NIL,"SUM",NIL,"CUSTO TOTAL REQ PODER 3 ------->",PesqPict("SD3","D3_CUSTO1",17),{|| If((cQryOP)->ISMOD == 'N' .And. SubStr((cQryOP)->D3_CF,1,2)=="RE" .And. SubStr(D3_CF,3,1)=="9", oSection1:Cell('Custo'   ):GetValue(), 0) },.F.,.T.) //"CUSTO TOTAL REQ PODER 3 ------->"

oFunction := TRFunction():New(oSection1:Cell('D3_QUANT'),NIL,"SUM",NIL,"QTDE. TOTAL DEV PODER 3 ------->",/*Picture*/,{|| If((cQryOP)->ISMOD == 'N' .And. SubStr((cQryOP)->D3_CF,1,2)=="DE" .And. SubStr(D3_CF,3,1)=="9", (- oSection1:Cell('D3_QUANT'):GetValue()), 0) },.F.,.T.) //"QTDE. TOTAL DEV PODER 3 ------->"
oFunction := TRFunction():New(oSection1:Cell('Custo'   ),NIL,"SUM",NIL,"CUSTO TOTAL DEV PODER 3 ------->",PesqPict("SD3","D3_CUSTO1",17),{|| If((cQryOP)->ISMOD == 'N' .And. SubStr((cQryOP)->D3_CF,1,2)=="DE" .And. SubStr(D3_CF,3,1)=="9", (- oSection1:Cell('Custo'   ):GetValue()), 0) },.F.,.T.) //"CUSTO TOTAL DEV PODER 3 ------->"

oFunction := TRFunction():New(oSection1:Cell('D3_QUANT'),NIL,"SUM",NIL,"QTDE. TOTAL REQ. MAO DE OBRA -->",/*Picture*/,{|| If((cQryOP)->ISMOD == 'S' .And. SubStr((cQryOP)->D3_CF,1,2)=="RE" .And. SubStr(D3_CF,3,1) # "9", oSection1:Cell('D3_QUANT'):GetValue(), 0) },.F.,.T.) //"QTDE. TOTAL REQ. MAO DE OBRA -->"
oFunction := TRFunction():New(oSection1:Cell('Custo'   ),NIL,"SUM",NIL,"CUSTO TOTAL REQ. MAO DE OBRA -->",PesqPict("SD3","D3_CUSTO1",17),{|| If((cQryOP)->ISMOD == 'S' .And. SubStr((cQryOP)->D3_CF,1,2)=="RE" .And. SubStr(D3_CF,3,1) # "9", oSection1:Cell('Custo'   ):GetValue(), 0) },.F.,.T.) //"CUSTO TOTAL REQ. MAO DE OBRA -->"

oFunction := TRFunction():New(oSection1:Cell('D3_QUANT'),NIL,"SUM",NIL,"QTDE. TOTAL DEV. MAO DE OBRA -->",/*Picture*/,{|| If((cQryOP)->ISMOD == 'S' .And. SubStr((cQryOP)->D3_CF,1,2)=="DE" .And. SubStr(D3_CF,3,1) # "9", (- oSection1:Cell('D3_QUANT'):GetValue()), 0) },.F.,.T.) //"QTDE. TOTAL DEV. MAO DE OBRA -->"
oFunction := TRFunction():New(oSection1:Cell('Custo'   ),NIL,"SUM",NIL,"CUSTO TOTAL DEV. MAO DE OBRA -->",PesqPict("SD3","D3_CUSTO1",17),{|| If((cQryOP)->ISMOD == 'S' .And. SubStr((cQryOP)->D3_CF,1,2)=="DE" .And. SubStr(D3_CF,3,1) # "9", (- oSection1:Cell('Custo'   ):GetValue()), 0) },.F.,.T.) //"CUSTO TOTAL DEV. MAO DE OBRA -->"

// Inibindo celulas, utilizadas apenas para totalizadores
If oReport:nDevice != 4 .Or. (oReport:nDevice == 4 .And. !oReport:lXlsTable .And. oReport:lXlsHeader)  //impressao em planilha tipo tabela
	oSection1:Cell('B1_CUSTD'):Hide()
	oSection1:Cell('B1_CUSTD'):HideHeader()
	oSection1:Cell('TotalSTD'):Hide()
	oSection1:Cell('TotalSTD'):HideHeader()
EndIf

// Define o campo a ser impresso no valor de acordo com a moeda selecionada
Do Case
	Case mv_par03 == 1
		If lCusRep .And. mv_par07==2
			cCampoCus :=   "(cQryOP)->D3_CUSRP1"
		Else
			cCampoCus :=   "(cQryOP)->D3_CUSTO1"
		EndIf
	Case mv_par03 == 2
		If lCusRep .And. mv_par07==2
			cCampoCus :=   "(cQryOP)->D3_CUSRP2"
		Else
			cCampoCus :=   "(cQryOP)->D3_CUSTO2"
		EndIf
	Case mv_par03 == 3
		If lCusRep .And. mv_par07==2
			cCampoCus :=   "(cQryOP)->D3_CUSRP3"
		Else
			cCampoCus :=   "(cQryOP)->D3_CUSTO3"
		EndIf
	Case mv_par03 == 4
		If lCusRep .And. mv_par07==2
			cCampoCus :=   "(cQryOP)->D3_CUSRP4"
		Else
			cCampoCus :=   "(cQryOP)->D3_CUSTO4"
		EndIf
	Case mv_par03 == 5
		If lCusRep .And. mv_par07==2
			cCampoCus :=   "(cQryOP)->D3_CUSRP5"
		Else
			cCampoCus :=   "(cQryOP)->D3_CUSTO5"
		EndIf
EndCase

// Inicio da impressao do fluxo do relatorio
oReport:SetMeter(SD3->(LastRec()))
oSection1:Init()
dbSelectArea(cQryOP)
While !oReport:Cancel() .And. !(cQryOP)->(Eof())
	cOpAnt := (cQryOP)->D3_OP
	While (cQryOP)->D3_FILIAL+(cQryOP)->D3_OP == xFilial("SD3")+cOpAnt
		oReport:Section(1):Cell('D3_OP'   ):SetValue( (cQryOP)->D3_OP )
		If ((cQryOP)->ISMOD == 'S')
			oReport:Section(1):Cell('D3_QUANT'):SetValue( If(SubStr((cQryOP)->D3_CF,1,2)=="DE",( -R860ToDec((cQryOP)->D3_QUANT )),R860ToDec((cQryOP)->D3_QUANT)) )
		Else
			oReport:Section(1):Cell('D3_QUANT'):SetValue( If(SubStr((cQryOP)->D3_CF,1,2)=="DE",( -(cQryOP)->D3_QUANT ),(cQryOP)->D3_QUANT) )
		EndIf

		// MATEUS HENGLE - 31/05/23
		oReport:Section(1):Cell('Unitario'):SetValue( (&(cCampoCus)/(cQryOP)->D3_QUANT) )
		// MATEUS HENGLE - 31/05/23

		//oReport:Section(1):Cell('Unitario'):SetValue( (&(cCampoCus)/oSection1:Cell('D3_QUANT'):GetValue()) )

		oReport:Section(1):Cell('Custo'   ):SetValue( If(SubStr((cQryOP)->D3_CF,1,2)=="DE",( -&(cCampoCus)),&(cCampoCus)) )
		If !(lCusRep .And. mv_par07==2)
			oReport:Section(1):Cell('B1_CUSTD'):SetValue( If(SubStr((cQryOP)->D3_CF,1,2)=="PR", RetFldProd((cQryOP)->B1_COD,"B1_CUSTD",cQryOP) ,0) )
		EndIf
		If !(lCusRep .And. mv_par07==2)
			oReport:Section(1):Cell('TotalSTD'):SetValue( If(SubStr((cQryOP)->D3_CF,1,2)=="PR",( oSection1:Cell('B1_CUSTD'):GetValue() * oSection1:Cell('D3_QUANT'):GetValue() ) , 0)  )
		EndIf
		If SubStr((cQryOP)->D3_CF,1,2) == "PR"
			oReport:SkipLine()
		EndIF
		If oReport:Cancel()
    	   	Exit
		EndIf
		oReport:IncMeter()
		oSection1:PrintLine()
		dbSkip()
	EndDo
	If lCusRep .And. mv_par07==2
		oReport:PrintText( cOpAnt+" - Valor final no ultimo fechamento ("+DTOC(dDataFec)+") => "+Transform(Posicione("SC2",1,xFilial("SC2")+cOpAnt,"C2_VFIMRP"+Str(MV_PAR03,1)),PesqPict("SC2","C2_VFIM1")) ) //" - Valor final no ultimo fechamento ("
	Else
		oReport:PrintText( cOpAnt+" - Valor final no ultimo fechamento ("+DTOC(dDataFec)+") => "+Transform(Posicione("SC2",1,xFilial("SC2")+cOpAnt,"C2_VFIM"+Str(MV_PAR03,1)),PesqPict("SC2","C2_VFIM1")) ) //" - Valor final no ultimo fechamento ("
	EndIf
	oReport:PrintText( "Produto : "+Posicione("SC2",1,xFilial("SC2")+cOpAnt,"C2_PRODUTO") ) // "Produto : "
EndDo
oSection1:Finish()
(cQryOP)->(DbCloseArea())
Return NIL

/*/{Protheus.doc} MATR860R3
Relacao Das Ordens de Producao - Impressao em modo grafico R3 (legado).

@type   Static Function
@author Eveli Morasco
@since  06/03/1993
@return NIL
@uses   Generico

Historico de alteracoes:
Programador     | Data       | BOPS    | Motivo da Alteracao
Marcelo Pim.    | 04/12/1997 | 09746A  | perg.total.mat.movimentacao p/OP (mv_par06)
Rodrigo Sart    | 24/03/1998 | 08957A  | Incl. Impressao das Qtds totais (mv_par06)
Rodrigo Sart    | 24/06/1998 | XXXXXX  | Acerto no tamanho do documento para 12 posicoes
Rodrigo Sart    | 04/11/1998 | XXXXXX  | Acerto p/ Bug Ano 2000
Fernando J.     | 02/12/1998 | 18752A  | A funcao PesqPictQT foi substituida pela PesqPict.
Cesar Valadao   | 31/03/1999 | XXXXXX  | Manutencao na SetPrint()
Cesar Valadao   | 20/05/1999 | XXXXXX  | Retirada Condicao Especifica p/ TOP IndRegua() - cCondicao
Patricia Sal    | 25/11/1999 | 25324A  | IndRegua()-cCondicao-Acerto para imprimir somente Op's e filtrar por SUBS(D3_CF,2,1)
/*/
Static Function MATR860R3()
Local titulo   	:= "Relacao por Ordem de Producao"	//"Relacao por Ordem de Producao"
Local cDesc1   	:= "O objetivo deste relatorio e exibir detalhadamente todas as movimenta-"	//"O objetivo deste relatorio e exibir detalhadamente todas as movimenta-"
Local cDesc2   	:= "coes feitas para cada Ordem de Producao, mostrando inclusive os custos."	//"coes feitas para cada Ordem de Producao, mostrando inclusive os custos."
Local cString  	:= "SD3"
Local wnrel		:= "MATR860"
Local Tamanho  	:= "G"

PRIVATE aReturn  	:= {"Zebrado",1,"Administracao", 2, 2, 1, "",1 }	//"Zebrado"###"Administracao"
PRIVATE nLastKey	:= 0
PRIVATE cPerg 		:= "MTR860"


// Verifica as perguntas selecionadas
//
// Variaveis utilizadas para parametros
// mv_par01     // OP inicial
// mv_par02     // OP final
// mv_par03     // moeda selecionada ( 1 a 5 )
// mv_par04     // De  Data Movimentacao
// mv_par05     // Ate Data Movimentacao
// mv_par06     // Totaliza Mov.do mat. movimentados pela O.P.
// mv_par07     // Qual Custo Imprimir ? ( Medio / Reposicao )
Pergunte(cPerg,.F.)

// Envia controle para a funcao SETPRINT
wnrel:=SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,"",.F.,"",,Tamanho)

If nLastKey = 27
	Set Filter To
	Return
EndIf

SetDefault(aReturn,cString)

If nLastKey = 27
	Set Filter To
	Return
EndIf

RptStatus({|lEnd| R860Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

Return NIL

/*/{Protheus.doc} R860Imp
Chamada do Relatorio (impressao R3).

@type   Static Function
@author Waldemiro L. Lustosa
@since  13/11/1995
@param  lEnd, Logical, Flag de cancelamento
@param  wnRel, Character, Nome do relatorio
@param  titulo, Character, Titulo do relatorio
@param  tamanho, Character, Tamanho da pagina
@return NIL
@uses   MATR860 / XMTR860
/*/
Static Function R860Imp(lEnd,wnRel,titulo,tamanho)

// Define Variaveis
Local cabec1,cabec2
Local cRodaTxt     := ''
Local cOpAnt       := ''
Local cCampoCus    := ''
Local cCondicao    := ''
Local nomeprog     := "MATR860"

Local nCntImpr     := 0
Local nTipo        := 0
Local nCusto       := 0
Local nTotQuant    := 0
Local nTotCusto    := 0
Local nTotReq      := 0
Local nTotProd     := 0
Local nTotDev      := 0
Local nTotReqTer   := 0
Local nTotDevTer   := 0
Local nTotQuantMod := 0
Local nTotCustoMod := 0
Local nTotReqMod   := 0
Local nTotDevMod   := 0
Local nQuantReq    := 0
Local nQuantProd   := 0
Local nQuantDev    := 0
Local nQuantDevTer := 0
Local nQuantReqTer := 0
Local nQtdReqMod   := 0
Local nQtdDevMod   := 0
Local aColPos      := {}
Local lContinua    := .T.

// Variaveis para controle do cursor de progressao do relatorio
Local nTotRegs := 0 ,nMult := 1 ,nPosAnt := 4 ,nPosAtu := 4 ,nPosCnt := 0
// Variaveis locais exclusivas deste programa
Local bBloco := { |nV,nX| Trim(nV)+IIf(Valtype(nX)='C',"",Str(nX,1)) }
// Variaveis utilizada para verificar ultimo fechamento estoque
Local dDataFec:= GETMV("MV_ULMES")
// Verifica o tamanho do codigo do produto
Local lNewTamProd := TamSX3("B1_COD")[1] > 15 // Verifica o tamanho do codigo do produto
// MV_CUSREP - Parametro utilizado para habilitar o calculo do
//             Custo de Reposicao.
Local lCusRep  := SuperGetMv("MV_CUSREP",.F.,.F.) .And. (MA330AvRep())
// Contadores de linha e pagina
PRIVATE li := 80 ,m_pag := 1
// Variaveis locais exclusivas deste programa
PRIVATE cNomArq := ''
// Verifica se deve comprimir ou nao
nTipo  := IIF(aReturn[4]==1,15,18)

// Alerta o usuario que o custo de reposicao esta desativado.
If mv_par07==2 .And. !lCusRep
	Help(" ",1,"A860CUSRP")
	mv_par07 := 1
EndIf

// Adiciona informacoes ao titulo do relatorio
If Type("NewHead")#"U"
	NewHead += " - "+AllTrim(GetMv("MV_SIMB"+Ltrim(Str(mv_par03))))
Else
	Titulo  += " - "+AllTrim(GetMv("MV_SIMB"+Ltrim(Str(mv_par03)))) + " / " + IIf(lCusRep .And. mv_par07==2,"Custo Reposicao","Custo Medio")
EndIf

// Monta os Cabecalhos conforme Tamanho Produto
cabec1 := IIf(lNewTamProd,"CENTRO               ORDEM DE    MOV CODIGO DO                      DESCRICAO                      QUANTIDADE         UM    CUSTO              C U S T O          NUMERO                   DATA DE","CENTRO               ORDEM DE    MOV CODIGO DO       DESCRICAO              QUANTIDADE    UM         CUSTO       C U S T O  NUMERO       DATA DE")	//"CENTRO               ORDEM DE    MOV CODIGO DO       DESCRICAO              QUANTIDADE    UM         CUSTO       C U S T O  NUMERO       DATA DE"
cabec2 := IIf(lNewTamProd,"CUSTO                PRODUCAO        PRODUTO                                                                                                     UNITARIO          T O T A L          DOCUMENTO                EMISSAO","CUSTO                PRODUCAO        PRODUTO                                                      UNITARIO       T O T A L  DOCUMENTO    EMISSAO")	//"CUSTO                PRODUCAO        PRODUTO                                                      UNITARIO       T O T A L  DOCUMENTO    EMISSAO"
*****											   12345678901234567890 12345612121 123 123456789012345 12345678901234567890 99,999,999.9999 12 99,999,999.99 9999,999,999.99 123456789012 12/12/1234
*****											   0         1         2         3         4         5         6         7         8         9        10        11        12        13
*****											   0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234

// Define o campo a ser impresso no valor de acordo com a moeda selecionada
Do Case
	Case mv_par03 == 1
		If lCusRep .And. mv_par07==2
			cCampoCus :=   "SD3->D3_CUSRP1"
		Else
			cCampoCus :=   "SD3->D3_CUSTO1"
		EndIf
	Case mv_par03 == 2
		If lCusRep .And. mv_par07==2
			cCampoCus :=   "SD3->D3_CUSRP2"
		Else
			cCampoCus :=   "SD3->D3_CUSTO2"
		EndIf
	Case mv_par03 == 3
		If lCusRep .And. mv_par07==2
			cCampoCus :=   "SD3->D3_CUSRP3"
		Else
			cCampoCus :=   "SD3->D3_CUSTO3"
		EndIf
	Case mv_par03 == 4
		If lCusRep .And. mv_par07==2
			cCampoCus :=   "SD3->D3_CUSRP4"
		Else
			cCampoCus :=   "SD3->D3_CUSTO4"
		EndIf
	Case mv_par03 == 5
		If lCusRep .And. mv_par07==2
			cCampoCus :=   "SD3->D3_CUSRP5"
		Else
			cCampoCus :=   "SD3->D3_CUSTO5"
		EndIf
EndCase

// Pega o nome do arquivo de indice de trabalho
cNomArq := CriaTrab("",.F.)

dbSelectArea("SD3")
// Cria o indice de trabalho
cCondicao := "D3_FILIAL == '"+xFilial("SD3")+"' .And. D3_OP >= '"+mv_par01+"'"
cCondicao += " .And. D3_OP <= '"+mv_par02+"' .And. D3_OP <> ' ' .And. DTOS(D3_EMISSAO) >= '"+DTOS(mv_par04)+"'.And. DTOS(D3_EMISSAO) <= '"+DTOS(mv_par05)+"'"

IndRegua("SD3",cNomArq,"D3_FILIAL+D3_OP+D3_CHAVE+D3_NUMSEQ+D3_COD",,cCondicao,"Selecionando Registros...")     //"Selecionando Registros..."
dbGoTop()

nTotReq     :=0
nTotReqTer  :=0
nTotDev     :=0
nTotDevTer  :=0
nTotProd    :=0
nTotReqMod  :=0
nTotDevMod  :=0
nTotReqMo3  :=0
nTotDevMo3  :=0
nQuantReq   :=0
nQuantReqTer:=0
nQuantProd  :=0
nQuantDev   :=0
nQuantDevTer:=0
nQtdReqMod  :=0
nQtdDevMod  :=0

// aColPos - Define o posicionamento das colunas
ny := 5
If lNewTamProd
	aColPos := {000,021,035,039,070,101,122,126,143,162,187,095,128,099,143}
Else
	aColPos := {000,021,035,039,055,086,102,106,123,142,166,080,113,084,123}
EndIf

SetRegua(LastRec())

// Correr SD3 para ler as REs, DEs e Producoes.
While lContinua .And. !Eof()

	If lEnd
		@ PROW()+1,001 PSay "CANCELADO PELO OPERADOR"	//"CANCELADO PELO OPERADOR"
		Exit
	EndIf
	IncRegua()

	// Correr SD3 para a mesma OP.
	nTotQuant    := 0
	nTotCusto    := 0
	nQtdeProd    := 0
	nTotQuantMod := 0
	nTotCustoMod := 0
	cOpAnt       := D3_OP
	While !Eof() .And. D3_FILIAL+D3_OP = xFilial()+cOpAnt

		If lEnd
			@ PROW()+1,001 PSay "CANCELADO PELO OPERADOR"	//"CANCELADO PELO OPERADOR"
			lContinua := .F.
			Exit
		EndIf

		IncRegua()

		If li > 58
			Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
		EndIf

		If D3_ESTORNO == "S"
			dbSkip()
			Loop
		EndIf

		nCusto := &(cCampoCus)
		If !IsProdMod(SD3->D3_COD)
			nTotQuant += IIf( SubStr(D3_CF,1,2) == "RE", D3_QUANT, 0 )
			nTotQuant += IIf( SubStr(D3_CF,1,2) == "DE", ( -D3_QUANT ), 0 )
			If SubStr(D3_CF,3,1) # "9"
				nTotCusto += IIf( SubStr(D3_CF,1,2) == "RE", nCusto, 0 )
				nTotCusto += IIf( SubStr(D3_CF,1,2) == "DE", ( -nCusto ), 0 )
			EndIf
		Else
			// Totalizacao separada para a mao-de-obra
			nTotQuantMod += IIf( SubStr(D3_CF,1,2) == "RE", D3_QUANT, 0 )
			nTotQuantMod += IIf( SubStr(D3_CF,1,2) == "DE", ( -D3_QUANT ), 0 )
			If SubStr(D3_CF,3,1) # "9"
				nTotCustoMod += IIf( SubStr(D3_CF,1,2) == "RE", nCusto, 0 )
				nTotCustoMod += IIf( SubStr(D3_CF,1,2) == "DE", ( -nCusto ), 0 )
			EndIf
		EndIf

		nQtdeProd += IIf( SubStr(D3_CF,1,2) == "PR", D3_QUANT , 0 )
		nQtdeProd += IIf( SubStr(D3_CF,1,2) == "ER", -D3_QUANT , 0 )

		dbSelectArea("SB1")
		dbSeek(cFilial+SD3->D3_COD)
		dbSelectArea("SD3")
		If SubStr(D3_CF,1,2) == "PR"
			Li++
		EndIf

		@ Li,aColPos[01] PSay D3_CC
		@ Li,aColPos[02] PSay D3_OP
		@ Li,aColPos[03] PSay D3_CF
		@ Li,aColPos[04] PSay D3_COD
		@ Li,aColPos[05] PSay SubStr(SB1->B1_DESC,1,30)
		If SubStr(D3_CF,1,2) == "DE"
			If IsProdMod(SD3->D3_COD)
				@ Li,aColPos[06] PSay ( -R860ToDec(D3_QUANT) )			Picture PesqPict("SD3","D3_QUANT" ,15)
			Else
				@ Li,aColPos[06] PSay ( -D3_QUANT )			Picture PesqPict("SD3","D3_QUANT" ,15)
			EndIf
			@ Li,aColPos[07] PSay D3_UM
			@ Li,aColPos[08] PSay ( nCusto/D3_QUANT )	Picture PesqPict("SD3","D3_CUSTO1",17)
			@ Li,aColPos[09] PSay ( -nCusto )			Picture PesqPict("SD3","D3_CUSTO1",17)
		Else
			If IsProdMod(SD3->D3_COD)
				@ Li,aColPos[06] PSay ( R860ToDec(D3_QUANT) )			Picture PesqPict("SD3","D3_QUANT" ,15)
			Else
				@ Li,aColPos[06] PSay ( D3_QUANT )			Picture PesqPict("SD3","D3_QUANT" ,15)
			EndIf
			@ Li,aColPos[07] PSay D3_UM
			@ Li,aColPos[08] PSay ( nCusto/D3_QUANT )	Picture PesqPict("SD3","D3_CUSTO1",17)
			@ Li,aColPos[09] PSay nCusto				Picture PesqPict("SD3","D3_CUSTO1",17)
		EndIf
		@ Li,aColPos[10] PSay Substr(D3_DOC,1,20)		Picture PesqPict("SD3","D3_DOC"	  ,12)
		@ Li,aColPos[11] PSay D3_EMISSAO
		Li++

		If !IsProdMod(SD3->D3_COD)
			If SubStr(D3_CF,1,2) == "RE"
				If SubStr(D3_CF,3,1) # "9"
					nTotReq		+= nCusto
					nQuantReq	+= D3_QUANT
				Else
					nTotReqTer	+= nCusto
					nQuantReqTer+= D3_QUANT
				EndIf
			Elseif SubStr(D3_CF,1,2) == "DE"
				If SubStr(D3_CF,3,1) # "9"
					nTotDev		+= nCusto
					nQuantDev	+= D3_QUANT
				Else
					nTotDevTer	+= nCusto
					nQuantDevTer+= D3_QUANT
				EndIf
			Endif
		Else
			// Totalizacao separada para a mao-de-obra
			If SubStr(D3_CF,1,2) == "RE"
				If SubStr(D3_CF,3,1) # "9"
					nTotReqMod	+= nCusto
				Else
					nTotReqMo3	+= nCusto
				EndIf
				nQtdReqMod	+= D3_QUANT
			Elseif SubStr(D3_CF,1,2) == "DE"
				If SubStr(D3_CF,3,1) # "9"
					nTotDevMod	+= nCusto
				Else
					nTotDevMo3	+= nCusto
				EndIf
				nQtdDevMod	+= D3_QUANT
			Endif
		EndIf

		If SubStr(D3_CF,1,2) == "PR"
			nTotProd		+= nCusto
			nQuantProd	+= D3_QUANT
		EndIf

		dbSkip()

	End

	Li++

	If (nTotQuant+nTotQuantMod) != 0
		@ Li,000 PSay "TOTAL  " + cOpAnt	//"TOTAL  "
		If !(lCusRep .And. mv_par07==2)
			@ Li,021 PSay "Custo STD : "			//"Custo STD : "
			@ Li,035 PSay RetFldProd(SB1->B1_COD,"B1_CUSTD") Picture PesqPict("SB1","B1_CUSTD",12)
			@ Li,047 PSay "/"
			@ Li,051 PSay ( RetFldProd(SB1->B1_COD,"B1_CUSTD") * nQtdeProd ) Picture PesqPict("SB1","B1_CUSTD",17)
		EndIf
		If mv_par06 == 1
			@ Li,aColPos[14] PSay nTotQuant	Picture PesqPict("SD3","D3_QUANT" ,17)
		Endif
		@ Li,aColPos[15] 	 PSay nTotCusto Picture PesqPict("SD3","D3_CUSTO1",17)
		Li++
		If nTotQuantMod <> 0 .Or. nTotCustoMod <> 0
			@ Li,000  PSay "       MAO DE OBRA:"	//"       MAO DE OBRA:"
			@ Li,aColPos[14] PSay R860ToDec(nTotQuantMod) Picture PesqPict("SD3","D3_QUANT" ,17)
			@ Li,aColPos[15] PSay nTotCustoMod Picture PesqPict("SD3","D3_CUSTO1",17)
			Li++
		Endif
	EndIf
	If SC2->(dbSeek(xFilial("SC2")+cOPAnt))
		@ li,000 PSay cOpAnt+" - Valor final no ultimo fechamento ("+DTOC(dDataFec)+")" //" - Valor final no ultimo fechamento ("
		If lCusRep .And. mv_par07==2
			@ li,aColPos[15] PSay &(Eval(bBloco,"SC2->C2_VFIMRP",mv_par03)) Picture PesqPict("SD3","D3_CUSTO1",17)
		Else
			@ li,aColPos[15] PSay &(Eval(bBloco,"SC2->C2_VFIM",mv_par03)) Picture PesqPict("SD3","D3_CUSTO1",17)
		EndIf
		li++
		@ li,000 PSay "Produto :"+" "+SC2->C2_PRODUTO
		li++
	EndIf

	@ Li,000 PSay Replicate("-",220)
	Li += 2

EndDo

If li != 80
	Li++
	@ Li,000 PSay "TOTAL REQUISICOES ---->"	//"TOTAL REQUISICOES ---->"
	If mv_par06 == 1
		@ Li,aColPos[12] PSay nQuantReq		Picture PesqPict("SD3","D3_QUANT" ,17)
	EndIf
	@ Li,aColPos[15] 	 PSay nTotReq		Picture PesqPict("SD3","D3_CUSTO1",17)
	Li++
	If li > 58
		Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
	EndIf
	@ Li,000 PSay "TOTAL PRODUCAO    ---->"	//"TOTAL PRODUCAO    ---->"
	If mv_par06 == 1
		@ Li,aColPos[12] PSay nQuantProd	Picture PesqPict("SD3","D3_QUANT" ,17)
	EndIf
	@ Li,aColPos[15] 	 PSay nTotProd		Picture PesqPict("SD3","D3_CUSTO1",17)
	Li++
	@ Li,000 PSay "TOTAL DEVOLUCOES  ---->"	//"TOTAL DEVOLUCOES  ---->"
	If mv_par06 == 1
		@ Li,aColPos[12] PSay nQuantDev		Picture PesqPict("SD3","D3_QUANT" ,17)
	EndIf
	@ Li,aColPos[15] 	 PSay nTotDev		Picture PesqPict("SD3","D3_CUSTO1",17)
	Li++
	If li > 57
		Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
	EndIf

	@ Li,000 PSay "TOTAL REQ PODER 3 ---->"	// "TOTAL REQ PODER 3 ---->"
	If mv_par06 == 1
		@ Li,aColPos[12] PSay nQuantReqTer	Picture PesqPict("SD3","D3_QUANT" ,17)
	EndIf
	@ Li,aColPos[15] 	 PSay nTotReqTer	Picture PesqPict("SD3","D3_CUSTO1",17)
	Li++
	If li > 57
		Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
	EndIf

	@ Li,000 PSay "TOTAL DEV PODER 3 ---->"	//"TOTAL DEV PODER 3 ---->"
	If mv_par06 == 1
		@ Li,aColPos[12] PSay nQuantDevTer	Picture PesqPict("SD3","D3_QUANT" ,17)
	EndIf
	@ Li,aColPos[15] 	 PSay nTotDevTer	Picture PesqPict("SD3","D3_CUSTO1",17)
	Li++
	If li > 57
		Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
	EndIf

	If nTotReqMod <> 0
		@ Li,000 PSay "TOTAL REQUISICOES MAO DE OBRA ---->"	//"TOTAL REQUISICOES MAO DE OBRA ---->"
		If mv_par06 == 1
			@ Li,aColPos[12] PSay nQtdReqMod	Picture PesqPict("SD3","D3_QUANT" ,17)
		EndIf
		@ Li,aColPos[15] 	 PSay nTotReqMod   	Picture PesqPict("SD3","D3_CUSTO1",17)
		Li++
	EndIf
	If nTotDevMod <> 0
		@ Li,000 PSay "TOTAL DEVOLUCOES  MAO DE OBRA ---->"	//"TOTAL DEVOLUCOES  MAO DE OBRA ---->"
		If mv_par06 == 1
			@ Li,aColPos[12] PSay nQtdDevMod	Picture PesqPict("SD3","D3_QUANT" ,17)
		EndIf
		@ Li,aColPos[15] 	 PSay nTotDevMod   	Picture PesqPict("SD3","D3_CUSTO1",17)
		Li++
	Endif
	Roda(nCntImpr,cRodaTxt,Tamanho)
EndIf

// Devolve as ordens originais do arquivo
RetIndex("SD3")

// Apaga indice de trabalho
cNomArq += OrdBagExt()
FErase( cNomArq )

// Devolve a condicao original do arquivo principal
dbSelectArea("SD3")
RetIndex("SD3")
Set Filter To
dbSetOrder(1)

If aReturn[5] = 1
	Set Printer To
	dbCommitAll()
	OurSpool(wnrel)
Endif

MS_FLUSH()

Return NIL

/*/{Protheus.doc} R860ToDec
Converte valor centesimal para decimal.

@type   Static Function
@author Luiza Liebl
@since  13/05/2019
@param  nValor, Numeric, Valor a converter
@return nTotal, Numeric, Valor convertido
@uses   MATR860 / XMTR860
/*/
Static Function R860ToDec(nValor)
Local nHoras
Local nMinutos
Local nTotal

	If(GetMv("MV_TPHR")=="N")
		nHoras := Int(nValor)
		nMinutos := (((nValor-nHoras)*60)/100)
		nTotal := nHoras + nMinutos
	Else
		nTotal := nValor
	EndIf

return nTotal
